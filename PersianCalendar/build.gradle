apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.2"

    defaultConfig {
        applicationId "com.byagowi.persiancalendar"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 594
        versionName "5.9.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            versionNameSuffix "-" + "git rev-parse --abbrev-ref HEAD".execute().text.trim() + "-" +
                    "git rev-list HEAD --count".execute().text.trim() + "-" +
                    "git rev-parse --short HEAD".execute().text.trim()
        }
        release {
            minifyEnabled true
            shrinkResources true
            // Maybe proguard-android-optimize.txt in future
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    dataBinding {
        enabled = true
    }
}

ext {
    versions = [
            androidX  : '1.0.0-rc02',
            dagger    : '2.16',
            junit     : '4.12',
            leakCanary: '1.6.1'
    ]
}

dependencies {
    implementation "androidx.appcompat:appcompat:$versions.androidX"
    implementation "androidx.preference:preference:$versions.androidX"
    implementation "androidx.recyclerview:recyclerview:$versions.androidX"
    implementation "androidx.cardview:cardview:$versions.androidX"
    implementation "com.google.android.material:material:$versions.androidX"
    implementation 'com.google.android:flexbox:1.1.0-beta1'
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'

    // Please apply this https://issuetracker.google.com/issues/112877717 before enabling it again
    // implementation 'android.arch.work:work-runtime:1.0.0-alpha07'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$versions.leakCanary"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$versions.leakCanary"

    annotationProcessor "com.google.dagger:dagger-compiler:$versions.dagger"
    implementation "com.google.dagger:dagger-android:$versions.dagger"
    annotationProcessor "com.google.dagger:dagger-android-processor:$versions.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.dagger"
    
    testImplementation "junit:junit:$versions.junit"

    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test:rules:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
}
